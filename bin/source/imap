#!/usr/bin/perl -w
use 5.010;
use strict;
use warnings;
use autodie;

use IO::Socket::SSL qw(debug1);
use Net::IMAP::Client;
use Term::ReadKey;

use App::Exobrain::Bus;
use App::Exobrain::Message;

my $user    = shift || die "Usage: $0 user server";
my $server  = shift || die "Usage: $0 user server";

say "Password?";

ReadMode('noecho');
my $password = ReadLine(0);
ReadMode(0); # Reset term
chomp($password);

my $imap = Net::IMAP::Client->new(
    server => $server,
    user   => $user,
    pass   => $password,
    ssl    => 1,
    port   => 993,
) or die "Could not connect to IMAP server";

$imap->login or die "Can't login " . $imap->last_error;

say "Connected...";

my $bus = App::Exobrain::Bus->new(
    type => 'PUB',
);

my $sent_box;

# These really should be configured or auto-sniffed on a per-server
# baiss.

if ($server =~ /gmail/) { $sent_box = '[Gmail]/Sent Mail'; }
else                    { $sent_box = 'Sent'; }

while (1) {

    measure($imap, 'INBOX');
    measure($imap, $sent_box);

    sleep(60);
}

sub measure {
    my ($imap, $mailbox) = @_;

    my $data = $imap->status($mailbox);

    my $count = $data->{MESSAGES};

    my $msg = App::Exobrain::Message->new(
        namespace => 'EMAIL',
        timestamp => time(),
        source    => 'IMAP',
        data      => { count => $count, user => $user, server => $server, mailbox => $mailbox },
        raw       => $data,
        summary   => "$user on $server has $count messages in $mailbox",
    )->send( $bus->_socket );

}
