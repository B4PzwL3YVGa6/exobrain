#!/usr/bin/perl
use 5.010;
use strict;
use warnings;
use autodie qw(:all);
use WWW::Mechanize;
use Config::Tiny;
use Try::Tiny;
use JSON::Any;
use Data::Dumper;
use App::Exobrain::Bus;
use App::Exobrain::Message;
use utf8::all;

use constant DEBUG => 1;

my $config = Config::Tiny->read("$ENV{HOME}/.rtbmrc");
my $TOKEN  = "oauth_token=$config->{FourSquare}{auth_token}&v=20130425";
my $BASE   = 'https://api.foursquare.com/v2';

my $mech = WWW::Mechanize->new();
my $json = JSON::Any->new;

my $bus  = App::Exobrain::Bus->new( type => 'PUB' );
my $time = time() - 86400;

while (1) {
    my $checktime = time();

    try {
        $mech->get("$BASE/checkins/recent?afterTimestamp=$time&$TOKEN");
    }
    catch {
        warn "No connection, or connection failed: $_";
        sleep(60);
    };

    $time = $checktime;

    my $checkins = $json->decode($mech->content)->{response}{recent};

    # Checkins come in most-recent first, so we reverse them and make
    # them chronological.

    foreach my $checkin (reverse @$checkins) {
        my $name = ( $checkin->{user}{firstName} // "" ) . " "
                 . ( $checkin->{user}{lastName}  // "" );

        my $time = localtime($checkin->{createdAt});

        my $summary = "$name is at $checkin->{venue}{name}";

#        $bus->send( "[$time] $name -> $checkin->{venue}{name}" );

        # say Dumper $bus->_socket;

        my $msg = App::Exobrain::Message->new(
            namespace => 'GEO',
            timestamp => $checkin->{createdAt},
            source    => 'FOURSQUARE',
            data      => $checkin,
            raw       => $checkin,
            summary   => $summary,
        )->send( $bus->_socket );
    }

    sleep(60);
}
